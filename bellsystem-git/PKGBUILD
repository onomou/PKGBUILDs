# Maintainer: Garrett <floft.net/contact>
pkgname=bellsystem-git
pkgver=20120815
pkgrel=1
pkgdesc="A simple bell system written in C++ with a web UI in PHP."
arch=('arm' 'i686' 'x86_64')
url="http://floft.net/wiki/Bells.html"
license=('ISC')
depends=('glibc' 'libxml++' 'apache' 'php-apache' 'php')
optdepends=('autossh')
makedepends=('git')
backup=('etc/conf.d/bellsystem' 'usr/share/webapps/bellsystem/config.xml')
install=bellsystem.install

_gitroot="git://github.com/floft/BellSystem.git"
_gitname="bellsystem"

build() {
	cd "$srcdir"
	msg "Connecting to GIT server..."

	if [[ -d $_gitname ]]; then
		cd "$_gitname/"
		git pull origin
		msg "The local files have been updated."
		cd "$srcdir/"
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting make..."

	rm -rf "$_gitname-build"
	cp -r "$_gitname" "$_gitname-build"
	cd "$_gitname-build"
	
	make
}

package() {
	cd "$srcdir/$_gitname-build/"

	make PREFIX=/usr DESTDIR="$pkgdir" install

	mkdir -p "$pkgdir/etc/rc.d/"
	mkdir -p "$pkgdir/etc/conf.d/"
	mkdir -p "$pkgdir/etc/httpd/conf/extra/"
	mkdir -p "$pkgdir/usr/share/bellsystem/"
	mkdir -p "$pkgdir/usr/share/webapps/bellsystem/"

	install -Dm755 "install/bellsystem.daemon"          "$pkgdir/etc/rc.d/bellsystem"
	install -Dm755 "install/bell-http.daemon"           "$pkgdir/etc/rc.d/bell-http"
	install -Dm755 "install/bell-ssh.daemon"            "$pkgdir/etc/rc.d/bell-ssh"
	install -Dm644 "install/bellsystem.conf"            "$pkgdir/etc/conf.d/bellsystem"
	install -Dm644 "install/httpd-bellsystem.conf"      "$pkgdir/etc/httpd/conf/extra/"
	install -Dm644 "install/httpd-bellsystem-root.conf" "$pkgdir/etc/httpd/conf/extra/"
	install -Dm755 "install/password.sh"                "$pkgdir/usr/share/bellsystem/"

	cp -r www/.htaccess www/* "$pkgdir/usr/share/webapps/bellsystem/"
}
